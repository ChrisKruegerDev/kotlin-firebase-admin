buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        google()
        jcenter()
        maven { url = "https://plugins.gradle.org/m2/"}
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

plugins {
    id 'org.jetbrains.kotlin.js' version '1.3.72'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url = "https://dl.bintray.com/chrisbln/maven" }
    }
}

def projectVersion = "1.1.0"

version projectVersion
group "com.chrisbln"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js"
    implementation "com.chrisbln:kotlin-express:1.0.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-js"
}

task sourcesJar(type: Jar) {
    from "$projectDir/src"
    classifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.kotlin
            groupId "com.chrisbln"
            artifactId "kotlin-firebase-functions"
            version projectVersion
            artifact sourcesJar

            pom {
                name = 'kotlin-firebase-functions'
                description = 'Firebase Functions Wrapper for Kotlin/JS'
                url = 'https://github.com/chrisbln/kfirebase'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/chrisbln/kotlin-firebase-functions'
                }
                developers {
                    developer {
                        id = 'chrisbln'
                        name = 'Christian Krueger'
                        email = 'christian.krueger@moviebase.app'
                    }
                }
            }
        }
    }
}

apply from: 'bintray.gradle'
